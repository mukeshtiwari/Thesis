\chapter{Machine Checked Schulze Properties}
\label{cha:machine_checked}

\epigraph{Stay Hungry. Stay Foolish.} 
{\textit{Steve Jobs}} 


 Since the beginning of democracy, social scientist are constantly looking for methods  which would 
 aggregate the individual choice  to arrive at acceptable group decisions. 
 In general, these acceptable group decisions were based on intuition of the society at that time,
 but not backed by mathematical theory. The first mathematical 
 treatment to combine the individual choices (social mathematics)  can be 
 attributed to french philosopher and mathematician Marquis de Condorcet (Condorcet method, 1785) and his contemporary
 and co-national mathematician Jean-Charles de Borda (Borda count, 1770). However, the first formal system, foundational cornerstone
 of modern social choice theory, for collective preference was given by Kenneth Arrow. In 1950, Kenneth Arrow 
 published a paper titled \textit{A Difficulty in the Concept of Social Welfare} \citep{arrow1950difficulty}. 
 In this paper, Kenneth Arrow envisioned an axiomatic system having the following properties:
 
 \begin{itemize}
 \item Unrestricted domain
 \item Non-dictatorship
 \item Pareto efficiency
 \item Independence of irrelevant alternatives
 \end{itemize}
 
 Moreover, he showed no preferential voting method which can combine or aggregate the individual choices into a community wide 
 ranking would have all the properties of his axiomatic system. This result is now known as \textit{Arrow's impossibility theorem}. 


 In the light of impossibility theorem, Schulze method, a preferential voting method, can not have all the properties, and it fails on 
 Independence of irrelevant alternatives (IIA) criterion. Despite the fact that Schulze method fails on IIA,  it has plenty of other desirable
 properties established in the social choice theory. In this chapter, we will discuss some of 
 the properties.  Moreover, we will show that our implementation adheres to these properties. 
 
 \section{Condorcet Winner}
	A \textit{Condorcet winner} is a candidate who beats every other candidate in pairwise comparison (also known as head to head competition). 
	Recall that in Schulze method, the pairwise comparison method was margin function, denoted as $marg$, which defined as:
	\begin{displayquote}
	Given a set of ballots $P$ and candidate set $C$, we construct graph $G$ based on the margin function $marg: C \times C \to \mathbb{Z}$. Given two candidates $c, d \in C$,
the \emph{margin} of $c$ over $d$ is
the number of voters that prefer $c$ over $d$, minus the number of voters that prefer $d$ over $c$. 
In symbols:
\[
  marg(c, d) = \sharp \lbrace b \in P \mid c >_b d \rbrace -
            \sharp \lbrace b \in P \mid d >_b c \rbrace
\] where $\sharp$ denotes cardinality and $>_b$ is the strict
(preference) ordering given by the ballot $b \in P$.
 
 \end{displayquote}
  
	 
  Now we define the \textit{Condorcet winner} in Coq as:
 \begin{minted}{coq}
Definition condorcet_winner (c : cand) 
  (marg : cand -> cand -> Z) := forall d, marg c d >= 0.
\end{minted}

  Informally, the definition, \textit{condorcet\_winner}, states that 
  if a candidate $c$  is \textit{condorcet winner}, then she has been ranked higher against
  every other candidate.  Having the definition of  condorcet winner, our goal is to concluded that if there is 
 	a condorcet winner, the Schulze methods always elects it as a winner.  
 	
\begin{minted}{coq}
(* if candidate c is condorcet winner then it's winner of election *)
 Lemma condorcet_winner_implies_winner (c : cand) 
    (marg : cand -> cand -> Z) :  condorcet_winner c marg ->
    c_wins marg c = true. 
Proof.
      intros Hc. 
      pose proof  condorcet_winner_genmarg.
      pose proof c_wins_true.
      apply H0. intros d.
      pose proof (H c d (length cand_all) marg Hc).
      auto.
Qed.
\end{minted}
 		
 The proof of this theorem hinges on the two key facts:
 \begin{enumerate}
  \item If a canddiate beats everyone in pairwise comparison, then generalized margin between her and every other candidate would 
      be  greater than or equal to 0.
  \item If a canddiate beats everyone in pairwise comparison, then generalized margin between every other candidate and her would 
      be  less than or equal to 0.
 \end{enumerate}
 
 
 It is not very hard to see these two facts based on the definition of generalized margin. Intuitively, 
 if a candidate $c$ is the condorcet winner, then the strongest path between her and every other 
 candidate, say $d$,  would be either a direct path, $marg$ $c$ $d$, or a more stronger path, $M (c, d)$, 
 via some other intermediate candidates. 
  
 \begin{displayquote}
 A directed \emph{path} in the graph, $G$, from
candidate $c$ to candidate $d$ is a sequence $p \equiv c_0, \dots, c_{n+1}$
of candidates with $c_0 = c$ and $c_{n+1} = d$ ($n \geq 0$), and the
\emph{strength}, st, of path, p, is the minimum margin of adjacent
nodes, i.e.
\[ st(c_0, \dots, c_{n+1}) = \min \lbrace m (c_i, c_{i+1}) \mid 0
\leq i \leq n \rbrace. \]
\item For candidates c and d, let $M(c, d)$ denote the maximum strength, or generalized margin of a path
	from c to d i.e. 
	\[ M(c, d) = \max \lbrace st (p) : \text{p is path from c to d in G} \rbrace\]
  
   \end{displayquote}
   
 We capture these two facts in Coq:
 
 \begin{minted}{coq}
 Lemma gen_marg_gt0 :
   forall c d n marg, 
   condorcet_winner c marg -> 
   M marg n c d >= 0.
Proof. 
   unfold condercet_winner.
   intros c d n marg Hc.
   rewrite M_M_new_equal. 
   revert d; revert n.
   induction n; cbn; try auto.
   intros d. pose proof (IHn d).
   lia.
Qed.

Lemma gen_marg_lt0 :
   forall c d n marg , 
   condorcet_winner c marg ->
   M marg n d c <= 0.
Proof.
  unfold condercet_winner.
  intros c d n marg Hc.
  rewrite M_M_new_equal.
  revert d; revert n.
  induction n.
  + cbn. intros d. pose proof (marg_neq c d marg).
      pose proof (Hc d). lia. 
   + cbn. intros d.
       apply Z.max_lub_iff. 
       split.
       pose proof (IHn d). lia.
       apply upperbound_of_nonempty_list; try auto.
       intros x Hx. pose proof (IHn x).
       lia.
Qed.
\end{minted}   
 
 Using these two key facts, we concluded that for any condorcet winner candidate $c$, 
 the generalized margin between her and every other opponent is greater than or equal 
 to reverse generalized margin between every other candidate and her.  Formally, 
 in Coq we prove the following theorem, \textit{condorcet\_winner\_genmarg}, on which  the proof of 
 \textit{condorcet\_winner\_implies\_winner} hinges. 
 
\begin{minted}{coq}
 Lemma condorcet_winner_genmarg :
      forall c d n marg, 
      condorcet_winner c marg -> 
      M marg n d c <= M marg n c d.  
 Proof.
   intros c d n marg Hc.
   pose proof (gen_marg_gt0 c d n marg Hc).
   pose proof (gen_marg_lt0 c d n marg Hc).
   lia.
 Qed.
\end{minted}
   
\section{Reversal Symmetry}
 The \textit{Reversal symmetry} is a voting method criterion which states that if the voting method has produced a unique 
 winner, say $c$, based on the cast ballots, then $c$ should not be elected if the individual choices were 
 reversed. In context of Schulze method, we first need to define the unique winner, and ballot reversal. 
 
 \begin{minted}{coq}
 Definition unique_winner 
 (marg : cand -> cand -> Z) (c : cand) :=
  c_wins marg c = true /\
  (forall d, d <> c -> c_wins marg d = false).
\end{minted}  

Informally, our definition of \textit{unique\_winner} states that the candidate $c$ is a unique winner
if it wins the election with respect to computed margin function, $marg$, and every other candidate 
other than $c$ loses the election. 

We capture the ballot reversal in terms of margin function. For any given ballot set, if the computed 
margin between two candidates $c$ and $d$ is: 
\[
  marg(c, d) = \sharp \lbrace b \in P \mid c >_b d \rbrace -
            \sharp \lbrace b \in P \mid d >_b c \rbrace
\] 

If we reverse each ballot from the ballot set, then the new margin function, denoted as $rev\_marg$, would be:
\[
  rev\_marg(c, d) = -1 * marg (c, d)
\] 

This fact can also be demonstrated using a single ballot $ABC$. The interpretation is $A$ is strictly preferred over $B$, 
and $B$ is preferred over $C$ (but we don not need strict preferences to have this property). 
The margin function constructed from this ballot is: 

\[
\bordermatrix{ & A & B & C \cr
      A & 0 & 1 & 1 \cr
      B & -1 & 0 & 1 \cr
      C & -1 & -1 & 0 }
      \]
      
After reversing the original ballot, we get $CBA$ and the margin function is:
\[
\bordermatrix{ & A & B & C \cr
      A & 0 & -1 & -1 \cr
      B & 1 & 0 & -1 \cr
      C & 1 & 1 & 0 }
      \]

We capture this notion formally in Coq as:

\begin{minted}{coq}
Definition rev_marg 
   (marg : cand -> cand -> Z) (c d : cand) :=
   -marg c d.
\end{minted}

Based on the our definition of $rev\_marg$, we can formally state the reversal symmetry as:
\begin{minted}{coq}
Lemma winner_reversed :
      forall marg c, unique_winner marg c ->
      c_wins (rev_marg marg) c = false.
\end{minted}

The lemma, $winner\_reversed$, expresses that if a candidate $c$ is a unique winner 
with respect to $marg$ (computed from some ballot set $P$), then she is not a winner with respect 
to $rev\_marg$ (computed from reversing all the entries in the ballot set $P$).


The proof for this lemma is fairly intuitive, but it takes some efforts to prove it in Coq. 
In this lemma, we assume the 
existence of unique winner, say $c$ with respect to $marg$, which means that the generalize 
margin between her and every other candidate would be greater than the reverse generalized 
margin, i.e.  $\forall d$, $M \text{ marg } (c, d)$ > $M \text{ marg } (d, c)$. 
If we compute the generalize margin with respect to $rev\_marg$, then 
for the candidate $c$ it would be the case that:  
$\forall d$, $M \text{ rev\_marg } (c, d)$ <  $M \text{ rev\_marg } (d, c)$. 
One key observation is that the graph we get after computing the generalized margin with respect to $rev\_marg$ is 
simply a mirror image, every path is reversed, of the graph we get after computing the generalize margin with respect to $marg$. 
In terms of Coq, it is:
\begin{minted}{coq}
Lemma path_with_rev_marg :
  forall k marg c d,
  Path marg k c d <->  Path (rev_marg marg) k d c.
Proof.
  intros k marg c d.
  split. intro H. 
  destruct (path_iterated_marg marg k c d H) as [n Hn].
  destruct (proj1 (iterated_marg_char marg n c d k) Hn) 
    as [l [H1 H2]].
  rewrite  str_and_rev_str in H2.

  assert (length (rev l) <= n)%nat.
  rewrite rev_length. auto.
  pose proof (path_len_iterated_marg 
     (rev_marg marg) n d c k (rev l) H0 H2).
  pose proof (iterated_marg_path 
    (rev_marg marg) n k d c H3). auto. 
      
  intros H. 
  destruct (path_iterated_marg (rev_marg marg) k d c H) 
    as [n Hn].  
  destruct (proj1 (iterated_marg_char 
    (rev_marg marg) n d c k) Hn) as [l [H1 H2]].
  apply iterated_marg_path with (n := length l).
  apply path_len_iterated_marg with (l := rev l).
  rewrite rev_length. lia. 
  rewrite str_and_rev_str.
  rewrite rev_involutive. auto.
 Qed.
    
\end{minted}

Using the lemma $path\_with\_rev\_marg$, we can conclude that if the strength of 
a path going from $c$ to $d$ with respect to $marg$ is greater than or equal to $k$ 
(by definition of $Path$ inductive type), then the strength of a reverse path from $d$ to 
$c$ would also be greater than or equal to $k$ with respect $rev\_marg$. 
Using all this fact, and some auxiliary  lemma 
the proof of reversal symmetry is mere rewriting the facts
\footnote{At the time of writing this thesis, proof of reversal 
symmetry hinges on a auxiliary lemma which 
is fairly intuitive, but demands a lot of  Coq machinery (a typical situation 
in theorem proving). We are in the process of proving it}. 

 	
 
 \section{Summary}
 Although, we have just proved two properties of Schulze method, and so far, this chapter 
 is far from being complete. The rationale behind this chapter was to put forward the idea of 
 not only implementing the voting method and proving its correctness, but also proving that the implementation 
 follows the property of voting method. In the next chapter, I will conclude this thesis and some possible 
 direction for future work.  
 
 
 